apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "tw.dh46.android_tdd_learning_kotlin"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Day15_Gradle ProductFlavor
    // 每一個flavor都必須被assign到一個dimension
    // dimension 像是一個flavor group一樣
    flavorDimensions "version"

    productFlavors {
        // Mock 版
        mock {
            // Assigns this product flavor to the "version" flavor dimension.
            // If you are using only one dimension, this property is optional,
            // and the plugin automatically assigns all the module's flavors to
            // that dimension.
            dimension "version"
            applicationIdSuffix ".mock"
            versionNameSuffix "-mock"
        }

        // 正式版
        prod {

        }
    }

    // Robolectric Setup
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    // Day15_Gradle SourceSet
    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        String fakeJsonDir = 'src/sharedTest/fakejson'
        test {
            java.srcDir sharedTestDir
            resources.srcDirs += fakeJsonDir
        }
        androidTest {
            java.srcDir sharedTestDir
            resources.srcDirs += fakeJsonDir
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'

    // Espresso dependencies
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Mockito 測試框架
    testImplementation 'org.mockito:mockito-core:2.22.0'

    // Robolectric 測試框架
    testImplementation 'org.robolectric:robolectric:4.3.1'
}
